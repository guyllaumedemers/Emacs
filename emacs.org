
#+title: Emacs From Scratch Configuration
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el :mkdirp yes

* Welcome!

Here is a emacs config derived from systemcrafters.cc= that customize further your emacs experience in a MS Windows environment. *HOME* & *PATH* environment variables will be configure following the [[https://caiorss.github.io/Emacs-Elisp-Programming/Emacs_On_Windows.html][caiorss.github.io]] tutorial.

* Tips for Use

In this document I've added links in many places that lead you to documentation for the various packages we use.  If you're looking at this file in Emacs, you can put your cursor on a link and press =C-c C-o= or run =M-x org-open-at-point= to open the link in your web browser.

** Macros

A set of macros to be used in the current configuration.

*** get_regex_wildcard

A macro returning a short version of a generic regex prefix.

#+begin_src emacs-lisp

  (defun get_regex_wildcard (folder)
    (let ((result (format ".*%s.*\\(?:\\r\\|\\n\\|\\t\\)?" folder)))
      result))

#+end_src

*** get_system_path

A macro returning the system env path variables.

#+begin_src emacs-lisp

  (defun get_system_path ()
    (let ((result (cl-remove-duplicates (split-string (getenv "PATH") ";") :test 'string=))) result))

#+end_src

*** get_directories_using_regex

A macro returning the paths with the target in it.

#+begin_src emacs-lisp

  (defun get_directories_using_regex (target)
    (let (result)
      (dolist (directory (get_system_path) result)
        (if (string-match (get_regex_wildcard target) directory)
            (add-to-list 'result directory)))
      result))

#+end_src

*** get_file_using_regex

A macro returning the target file at path.

#+begin_src emacs-lisp

  (defun get_file_using_regex (target file)
    (let (result)
      (dolist (directory (get_directories_using_regex target) result)
        (dolist (elem (directory-files directory))
          (if (string-match file elem)
              (add-to-list 'result (format "%s\\%s" directory elem)))))
      result))

#+end_src

*** get_https_request

A macro downloading a file at url.

#+begin_src emacs-lisp

  (defun get_https_request (url filename)
    (url-copy-file url filename))

#+end_src

* System Config (Windows-nt)

Custom config variable that Unix based packages expect to exist.

** Env variables

Custom setup for various system variables.

*** Home

The HOME environment variable that has the default value /home/<username>) on Linux sets the user's directory path. This variable, which is equivalent to _%USERPROFILE% _(C:\\Users\<username> ) is expected to be set by many Unix applications ported to Windows and it also makes directory browser in Emacs and shells easier.

#+begin_src emacs-lisp

  (defun set-unix-system-home ()
    (interactive)
    (if (eq system-type 'windows-nt)
        (shell-command "setx HOME %HOME%")))

#+end_src

*** Path

Executables located in directories listed in PATH environment variable can be invoked without its full path like ls, echo and who in Unix-like OS or ipconfig, arp and whoami in Windows.

C:\Users\arch\bin> echo %PATH%
C:\ProgramData\Oracle\Java\javapath;C:\Windows\system32;C:\Windows;C:\Windows\System32\Wbem...

By adding the directory ~/bin or C:\Users\<user>\bin to PATH variable it makes easir to call command line applications from this directory without specifing its full path from Emacs or shell (cmd.exe).

#+begin_src emacs-lisp

  (defun set-unix-system-path ()
    (interactive)
    (if (eq system-type 'windows-nt)
        (shell-command "setx PATH \"%PATH%;%USERPROFILE%\\bin\"")))

#+end_src

*** Shell

Executables for bash. windows emacs shell behave like cmd which cause issue when running shell commands like echo, touch, etc...

#+begin_src emacs-lisp

  (defun set-unix-system-shell (exec)
    (interactive "sExecutable name: ")
    (when (eq system-type 'windows-nt)
      (let ((shell (car (get_file_using_regex "Git" (format "^%s" exec)))))
        (setq shell-file-name (format "%s" shell))
        (shell-command (format "setx SHELL \'%s\'" shell)))))

#+end_src

* Distribution Service

Set basic requirements for your operating system.

** Chocolatey (distribution service - package manager)

Chocolatey is a machine-level, command-line package manager and installer for software on Microsoft Windows. It uses the NuGet packaging infrastructure and Windows PowerShell to simplify the process of downloading and installing software.

#+begin_src emacs-lisp

  (defun install-chocolatey ()
    (interactive)
    (if (eq system-type 'windows-nt)
        (shell-command "Set-ExecutionPolicy Bypass -Scope Process -Force; [System.Net.ServicePointManager]::SecurityProtocol = [System.Net.ServicePointManager]::SecurityProtocol -bor 3072; iex ((New-Object System.Net.WebClient).DownloadString('https://community.chocolatey.org/install.ps1'))")))

#+end_src

** Tools

Set of tools, bash terminal, compilers and build system generator.

*** Git (distributed version control system)

Git is a distributed version control system that tracks changes in any set of computer files, usually used for coordinating work among programmers collaboratively developing source code during software development.

#+begin_src emacs-lisp

  (defun install-git ()
    (interactive)
    (shell-command "choco install git --params \"'/GitAndUnixToolsOnPath'\" --use-remembered-arguments -y"))

#+end_src

*** Gh CLI

gh cli allow creation of repo on the upstream using the command line. [gh repo create] for creating repository locally. [gh repo create my-project --public --clone] for creating a new remote repository and clone it locally. [gh repo create my-project --private --source=. --remote=upstream] for creating a remote repository from the current directory.

#+begin_src emacs-lisp

  (defun install-gh ()
    (interactive)
    (shell-command "choco install gh -y"))

#+end_src

*** MinGW

MinGW, formerly mingw32, is a free and open source software development environment to create Microsoft Windows applications. MinGW includes a port of the GNU Compiler Collection, GNU Binutils for Windows, a set of freely distributable Windows specific header files and static import libraries which enable the use of the Windows API, a Windows native build of the GNU Project's GNU Debugger, and miscellaneous utilities.

#+begin_src emacs-lisp

  (defun install-mingw ()
    (interactive)
    (shell-command "choco install mingw -y"))

#+end_src

*** CMake

CMake is cross-platform free and open-source software for build automation, testing, packaging and installation of software by using a compiler-independent method. CMake is not a build system itself; it generates another system's build files. [[https://github.com/chocolatey-community/chocolatey-packages/issues/987][Chocolatey CMAKE_PATH issue]]

#+begin_src emacs-lisp

  (defun install-cmake ()
    (interactive)
    (shell-command "choco install cmake --installargs '\"ADD_CMAKE_TO_PATH=User\"' --use-remembered-arguments -y"))

#+end_src

*** Make

Make is a build automation tool that automatically builds executable programs and libraries from source code by reading files called Makefiles which specify how to derive the target program.

#+begin_src emacs-lisp

  (defun install-make ()
    (interactive)
    (shell-command "choco install make -y"))

#+end_src

*** LLVM

LLVM is a set of compiler and toolchain technologies that can be used to develop a front end for any programming language and a back end for any instruction set architecture. LLVM is designed around a language-independent intermediate representation that serves as a portable, high-level assembly language that can be optimized with a variety of transformations over multiple passes.

#+begin_src emacs-lisp

  (defun install-llvm ()
    (interactive)
    (shell-command "choco install llvm -y"))

#+end_src

*** Visual C++ MSVC Building Tools

Calling the default and official Windows' C++ compiler MSVC (Microsoft Visual C++) from command line is not easy as calling gcc, mingw or clang as MSVC needs environment variables INCLUDE, LIB, LIBPATH and PATH to be properly set with correct paths.

#+begin_src emacs-lisp

  ;; set whenever you decide to actually use MSVC as compiler

#+end_src

** Languages

Some emacs packages rely on languages like python in order to properly work. The following programming languages are required to be install to prevent package errors. *See Messages buffer for more information on missing requirements*

*** Python3

Python is a high-level, general-purpose programming language. Python is dynamically typed and garbage-collected. It supports multiple programming paradigms, including structured, object-oriented and functional programming. 

#+begin_src emacs-lisp

  (defun install-python ()
    (interactive)
    (shell-command "choco install python -y"))

#+end_src

** Templates

A set of templates used when creating a new workspace directory.

*** Gitignore

#+begin_src emacs-lisp

  (defun init-gitignore ()
      (interactive)
      (get_https_request "https://github.com/guyllaumedemers/emacs-get-request/blob/master/.gitignore" ".gitignore"))

#+end_src

*** CMake Toolchain

CMake build system generator key steps to create build system required files.

***** Configuration

This stage is about reading project details stored in a directory, called the source tree, and preparing an output directory or build tree for the generation stage.

****** Running CMake Config Script

#+begin_src emacs-lisp

  (defun run-cmake-config ()
    (interactive)
    (shell-command "./cmake-config.sh"))

#+end_src

****** Building CMake Config Script

https://clang.llvm.org/docs/JSONCompilationDatabase.html

#+begin_src emacs-lisp

  (setq build_system "\'Unix Makefiles\'")
  (setq compilation_mode "\'DEBUG\'")
  (setq c_compiler "\'clang\'")
  (setq c++_compiler "\'clang++\'")

  (defun cmake-init-shell-config ()
    (interactive)
    ;; note: string output in .sh later invoke in bash shell so \ behaviour need to be handled twice
    (shell-command (format "echo cmake -S . -B \"\'out\\build\'\" -G \"%s\" -D CMAKE_BUILD_TYPE=\"%s\" -D CMAKE_C_COMPILER=\"%s\" -D CMAKE_CXX_COMPILER=\"%s\" -D CMAKE_EXPORT_COMPILE_COMMANDS=\'1\' -D CMAKE_CXX_EXTENSIONS=\'OFF\' -D CMAKE_CXX_STANDARD_REQUIRED=\'ON\' > cmake-config.sh" build_system compilation_mode c_compiler c++_compiler)))

#+end_src

***** Generation

After reading the project configuration, CMake will generate a *buildsystem* for the exact environment it is working in.

#+begin_src emacs-lisp

  ;; follows the configuration step without user interaction and generate Makefiles or project files

#+end_src

***** Building

Build tools will execute steps to produce *targets* with compilers, linkers, static and dynamic analysis tools, test frameworks, reporting tools, and anything else you can think of.

****** Running CMake Build Script

#+begin_src emacs-lisp

  (defun run-cmake-build ()
    (interactive)
    (shell-command "./cmake-build.sh"))

#+end_src

****** Building CMake Build Script

#+begin_src emacs-lisp

  (defun cmake-init-shell-build ()
    (interactive)
    (shell-command "echo cmake --build \"\'out\\build\'\" > cmake-build.sh"))

#+end_src

*** CMake Project

A few template files to properly start with CMake.

***** Modules

A set of modules to be used in cmake projects.

****** GitSubmodules

A git module that synch-update your project submodules.

#+begin_src emacs-lisp

  (defun cmake-init-git-module ()
    (interactive)
    (get_https_request "https://github.com/guyllaumedemers/emacs-get-request/blob/master/git.cmake" "git.cmake"))

#+end_src

***** Directory

A set of commands to generate your project directory tree.

****** Tree

Setup the current directory as follow:

#+begin_src ditaa

  root     
  |
  +--cmake
  |  +---*.cmake
  +--doc
  +--extern
  +--out
  |  +---build
  |  |   +----DEBUG
  |  |   +----RELEASE
  +--res
  +--src
  |  +---App1
  |  |   +---includes
  |  |   +---src
  |  |   +---test
  |  |   +---CMakeLists.txt   
  |  +---Main.cpp
  |  +---CMakeLists.txt
  +--test
  +--.git
  +--.gitsubmodules
  +--CMakeLists.txt
  +--README.md
  +--cmake-config.sh
  +--cmake-build.sh

#+end_src

****** Root

Run the following command at directory to create the above tree-like directories.

#+begin_src emacs-lisp

  (defun init-project-root-dir (&optional project)
    (interactive "sProject name: ")
    (shell-command (format "mkdir -p %s && cd %s && mkdir -p {cmake,doc,extern,out,res,src,test}" project project))
    (setq dir_folders (split-string (shell-command-to-string (format "cd %s && ls -d */" project)) "\n"))
    (dolist (elem dir_folders)
      (setq match (car (member elem (list "doc/" "extern/" "test/"))))
      (if match (shell-command (format "cd %s && touch > %s\\CMakeLists.txt" project match)))
      (shell-command (format "cd %s && touch > %s\\.keep" project elem))))

#+end_src

****** Application

Run the following command at directory to create the Application tree-like directories.

#+begin_src emacs-lisp

  (defun init-project-src-application-dir (&optional app)
    (interactive "sApplication name: ")
    (shell-command (format "mkdir -p %s && cd %s && mkdir -p {includes,lib3,test}" app app))
    (setq dir_folders (split-string (shell-command-to-string (format "cd %s && ls -d */" app)) "\n"))
    (dolist (elem dir_folders)
      (shell-command (format "cd %s && touch > %s\\.keep" app elem))))

#+end_src

***** CMakeLists.txt

A set of templates for generating listfile.

****** Root

Root listfile.

#+begin_src emacs-lisp

  (setq cmake_version (car (last (split-string (car (split-string (shell-command-to-string "cmake --version") "\n")) " "))))

  (defun cmake-init-root-cmakelist (project)
    (interactive "sProject name: ")
    (with-temp-file "CMakeLists.txt"
      (insert (format "cmake_minimum_required(VERSION %s)\nproject(%s VERSION 0.0.1)\n\nadd_subdirectory(\"doc\")\nadd_subdirectory(\"extern\")\nadd_subdirectory(\"src\" \"bin\")\nadd_subdirectory(\"test\")" cmake_version project))))

#+end_src

****** Source

Src listfile.

#+begin_src emacs-lisp

  (defun cmake-init-src-cmakelist (app)
    (interactive "sSubdirectory name: ")
    (with-temp-file "CMakeLists.txt"
      (insert (format "add_subdirectory(\"%s\")" app))))

#+end_src

****** Application

Application listfile.

#+begin_src emacs-lisp

  (defun cmake-init-app-cmakelist (app)
    (interactive "sApplication name: ")
    (with-temp-file "CMakeLists.txt"
      (insert (format "cmake_minimum_required(VERSION %s)\nproject(%s VERSION 0.0.1)\n\nfile(GLOB_RECURSE \"${PROJECT_NAME}_SRC\" \"${CMAKE_CURRENT_SOURCE_DIR}\" \"*.cc\")\n\nadd_library(\"${PROJECT_NAME}lib\" OBJECT \"${${PROJECT_NAME}_SRC}\")\ntarget_include_directories(\"${PROJECT_NAME}lib\" PUBLIC \"${CMAKE_CURRENT_SOURCE_DIR}/includes\")\n\n##temp executable\nget_target_property(OUT \"${PROJECT_NAME}lib\" SOURCES)\nadd_executable(\"${PROJECT_NAME}\" ${OUT})" cmake_version app))))

#+end_src

***** Main.cc

Hello World.cc

#+begin_src emacs-lisp

  (defun init-main-cc ()
    (interactive)
    (with-temp-file "Main.cc"
      (insert "#pragma once\n\n#include <cstdio>\n\nint main(int argc, char* argv[])\n{\n\tstd::printf(\"HelloWorld!\");\n\treturn 0;\n}")))

#+end_src

***** Master Command

A master command to easilly initialize your project with the correct startup files using the commands defined above.

#+begin_src emacs-lisp

  ;; spend time figuring out how to invoke emacs command from the shell terminal
  ;; without opening a new emacs frame

#+end_src

*** Debugging and Tracing

CMake can be run with a multitude of options that allow you to peek under the hood. To get general information about variables, commands, macro, and other settings.

#+begin_src emacs-lisp

  (defun run-cmake-debug-trace (&optional file)
    (interactive "sOutput file name: ")
    (shell-command (format "cmake --system-information %s" file)))

#+end_src

* Emacs System Config

A set of config specific to emacs that makes your life more pleasing.

** Basic UI Configuration

This section configures basic UI settings that remove unneeded elements to make Emacs look a lot more minimal and modern.  If you're just getting started in Emacs, the menu bar might be helpful so you can remove the =(menu-bar-mode -1)= line if you'd like to still see that.

#+begin_src emacs-lisp

  ;; set default emacs config
  (progn
    (setq inhibit-startup-message t)
    (scroll-bar-mode -1)        ; Disable visible scrollbar
    (tool-bar-mode -1)          ; Disable the toolbar
    (tooltip-mode -1)           ; Disable tooltips
    (set-fringe-mode 10)        ; Give some breathing room
    (menu-bar-mode -1)          ; Disable the menu bar
    (setq visible-bell t)       ; Set up the visible bell
    (setq use-file-dialog nil)
    (setq use-dialog-box nil)
    (add-to-list 'default-frame-alist '(fullscreen . maximized)) 
    (column-number-mode)
    (global-display-line-numbers-mode t)
    (setq-default buffer-file-coding-system 'utf-8-unix)
    ;; Disable line numbers for some modes
    (dolist (mode '(
                    org-mode-hook
                    term-mode-hook
                    shell-mode-hook
                    treemacs-mode-hook
                    eshell-mode-hook))
      (add-hook mode (lambda () (display-line-numbers-mode 0)))))

#+end_src

** Backup files

set temporary files to a different directory

#+begin_src emacs-lisp

  (setq backup-directory-alist `((".*" . ,temporary-file-directory)))

#+end_src

** Recycle Bin

The following line configures Emacs so that files deleted via Emacs are moved to the Recycle.

#+begin_src emacs-lisp

  (setq delete-by-moving-to-trash t) 

#+end_src

** Custom.el

set package customization to a different file to prevent poluting the init.el

#+begin_src emacs-lisp

  (progn
    (setq custom-file (expand-file-name "custom.el" user-emacs-directory))
    (when (file-exists-p custom-file)
      (load custom-file)))

#+end_src

* Package System Setup

Emacs has a built in package manager but it doesn't make it easy to automatically install packages on a new system the first time you pull down your configuration.  [[https://github.com/jwiegley/use-package][use-package]] is a really helpful package used in this configuration to make it a lot easier to automate the installation and configuration of everything else we use.

#+begin_src emacs-lisp

  (progn
    ;; init package sources
    (when (require 'package nil 'noerror)
      (setq package-archives '(
                               ("melpa" . "https://melpa.org/packages/")
                               ("org" . "https://orgmode.org/elpa/")
                               ("elpa" . "https://elpa.gnu.org/packages/")))
      (package-initialize)
      ;; refresh archive description list
      (unless package-archive-contents (package-refresh-contents))
      ;; install/update use-package
      (unless (package-installed-p 'use-package) (package-install 'use-package))
      ;; use-package ensure config
      (when (require 'use-package nil 'noerror)
        (setq use-package-always-ensure t))))

#+end_src

* Keybinding Configuration

** Evil

This configuration uses [[https://evil.readthedocs.io/en/latest/index.html][evil-mode]] for a Vi-like modal editing experience. [[https://github.com/emacs-evil/evil-collection][evil-collection]] is used to automatically configure various Emacs modes with Vi-like keybindings for evil-mode.

#+begin_src emacs-lisp

  ;; Make ESC quit prompts
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

  (use-package general
    :config
    (general-create-definer rune/leader-keys
      :keymaps '(normal insert visual emacs)
      :prefix "SPC"
      :global-prefix "C-SPC")
    (rune/leader-keys
      "t"  '(:ignore t :which-key "toggles")
      "tt" '(counsel-load-theme :which-key "choose theme"))) 

  (use-package evil
    :init
    (setq evil-want-integration t)
    (setq evil-want-keybinding nil)
    (setq evil-want-C-u-scroll t)
    (setq evil-want-C-i-jump nil)
    :config
    (evil-mode 1)
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
    (define-key evil-insert-state-map (kbd "C-h") 'evil-delete-backward-char-and-join)
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)
    (evil-set-initial-state 'messages-buffer-mode 'normal)
    (evil-set-initial-state 'dashboard-mode 'normal))

  (use-package evil-collection
    :config
    (evil-collection-init))

#+end_src

** Compilation Keybindings

Handy key bindings for invoking compilation command.

#+begin_src emacs-lisp

  (progn
    (global-set-key (kbd "<f9>") #'compile)
    (global-set-key (kbd "<C-f9>")
                    (lambda () (interactive)
                      (save-buffer)
                      (recompile))))

#+end_src

* UI Configuration

** Fonts

I am using the [[https://github.com/tonsky/FiraCode][Fira Code]] font for this configuration which will more than likely need to be installed on your machine.

#+begin_src emacs-lisp

  (progn
    (defvar efs/default-font-size 96)
    (defvar efs/default-variable-font-size 96)
    ;;choco install firacode
    (set-face-attribute 'default nil :font "Fira Code Retina" :height efs/default-font-size)
    (set-face-attribute 'fixed-pitch nil :font "Fira Code Retina" :height efs/default-font-size)
    (set-face-attribute 'variable-pitch nil :font "Fira Code Retina" :height efs/default-variable-font-size :weight 'regular))

#+end_src

** Color Theme

[[https://github.com/hlissner/emacs-doom-themes][doom-themes]] is a great set of themes with a lot of variety and support for many different Emacs modes.  Taking a look at the [[https://github.com/hlissner/emacs-doom-themes/tree/screenshots][screenshots]] might help you decide which one you like best.  You can also run =M-x counsel-load-theme= to choose between them easily.

#+begin_src emacs-lisp

  (use-package doom-themes
    :init
    (load-theme 'doom-dracula t))

#+end_src

** Better Modeline

[[https://github.com/seagle0128/doom-modeline][doom-modeline]] is a very attractive and rich (yet still minimal) mode line configuration for Emacs.  The default configuration is quite good but you can check out the [[https://github.com/seagle0128/doom-modeline#customize][configuration options]] for more things you can enable or disable.

*NOTE:* The first time you load your configuration on a new machine, you'll need to run `M-x all-the-icons-install-fonts` so that mode line icons display correctly.

#+begin_src emacs-lisp

  (use-package all-the-icons
    :if (display-graphic-p))

  (use-package all-the-icons-dired
    :hook
    (dired-mode . all-the-icons-dired-mode))

  (use-package doom-modeline
    :custom
    (doom-modeline-height 15)
    :init
    (doom-modeline-mode 1))

#+end_src

** Which Key

[[https://github.com/justbur/emacs-which-key][which-key]] is a useful UI panel that appears when you start pressing any key binding in Emacs to offer you all possible completions for the prefix.  For example, if you press =C-c= (hold control and press the letter =c=), a panel will appear at the bottom of the frame displaying all of the bindings under that prefix and which command they run.  This is very useful for learning the possible key bindings in the mode of your current buffer.

#+begin_src emacs-lisp

  (use-package which-key
    :diminish which-key-mode
    :init
    (which-key-mode)
    :config
    (setq which-key-idle-delay 1))

#+end_src

** Ivy and Counsel

[[https://oremacs.com/swiper/][Ivy]] is an excellent completion framework for Emacs.  It provides a minimal yet powerful selection menu that appears when you open files, switch buffers, and for many other tasks in Emacs.  Counsel is a customized set of commands to replace `find-file` with `counsel-find-file`, etc which provide useful commands for each of the default completion commands.

[[https://github.com/Yevgnen/ivy-rich][ivy-rich]] adds extra columns to a few of the Counsel commands to provide more information about each item.

#+begin_src emacs-lisp

  (use-package ivy
    :diminish
    :bind (("C-s" . swiper)
           :map ivy-minibuffer-map
           ("TAB" . ivy-alt-done)
           ("C-l" . ivy-alt-done)
           ("C-j" . ivy-next-line)
           ("C-k" . ivy-previous-line)
           :map ivy-switch-buffer-map
           ("C-k" . ivy-previous-line)
           ("C-l" . ivy-done)
           ("C-d" . ivy-switch-buffer-kill)
           :map ivy-reverse-i-search-map
           ("C-k" . ivy-previous-line)
           ("C-d" . ivy-reverse-i-search-kill))
    :config
    (ivy-mode 1))

  (use-package all-the-icons-ivy-rich
    :init
    (all-the-icons-ivy-rich-mode 1))

  (use-package ivy-rich
    :init
    (ivy-rich-mode 1))

  (use-package counsel
    :bind (("C-M-j" . 'counsel-switch-buffer)
           :map minibuffer-local-map
           ("C-r" . 'counsel-minibuffer-history))
    :config
    (counsel-mode 1))

#+end_src

** Helpful Help Commands

[[https://github.com/Wilfred/helpful][Helpful]] adds a lot of very helpful (get it?) information to Emacs' =describe-= command buffers.  For example, if you use =describe-function=, you will not only get the documentation about the function, you will also see the source code of the function and where it gets used in other places in the Emacs configuration.  It is very useful for figuring out how things work in Emacs.

#+begin_src emacs-lisp

  (use-package helpful
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key] . helpful-key))

#+end_src

* Org Mode

[[https://orgmode.org/][Org Mode]] is one of the hallmark features of Emacs.  It is a rich document editor, project planner, task and time tracker, blogging engine, and literate coding utility all wrapped up in one package.

** Better Font Faces

The =efs/org-font-setup= function configures various text faces to tweak the sizes of headings and use variable width fonts in most cases so that it looks more like we're editing a document in =org-mode=.  We switch back to fixed width (monospace) fonts for code blocks and tables so that they display correctly.

#+begin_src emacs-lisp

  (defun efs/org-font-setup ()
    ;; Replace list hyphen with dot
    (font-lock-add-keywords 'org-mode
                            '(("^ *\\([-]\\) "
                               (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

    ;; Set faces for heading levels
    (dolist (face '((org-level-1 . 1.2)
                    (org-level-2 . 1.1)
                    (org-level-3 . 1.05)
                    (org-level-4 . 1.0)
                    (org-level-5 . 1.1)
                    (org-level-6 . 1.1)
                    (org-level-7 . 1.1)
                    (org-level-8 . 1.1)))
      (set-face-attribute (car face) nil :font "Fira Code Retina" :weight 'regular :height (cdr face)))

    ;; Ensure that anything that should be fixed-pitch in Org files appears that way
    (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-code nil   :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-table nil   :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch))

#+end_src

** Basic Config

This section contains the basic configuration for =org-mode= plus the configuration for Org agendas and capture templates.  There's a lot to unpack in here so I'd recommend watching the videos for [[https://youtu.be/VcgjTEa0kU4][Part 5]] and [[https://youtu.be/PNE-mgkZ6HM][Part 6]] for a full explanation.

#+begin_src emacs-lisp

  (defun efs/org-mode-setup ()
    (org-indent-mode)
    (variable-pitch-mode 1)
    (visual-line-mode 1))

  (use-package org
    :hook
    (org-mode . efs/org-mode-setup)
    :config
    (setq org-ellipsis " ▾")
    (efs/org-font-setup))

#+end_src

*** Nicer Heading Bullets

[[https://github.com/sabof/org-bullets][org-bullets]] replaces the heading stars in =org-mode= buffers with nicer looking characters that you can control.  Another option for this is [[https://github.com/integral-dw/org-superstar-mode][org-superstar-mode]] which we may cover in a later video.

#+begin_src emacs-lisp

  (use-package org-bullets
    :custom
    (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●"))
    :hook
    (org-mode . org-bullets-mode))

#+end_src

*** Center Org Buffers

We use [[https://github.com/joostkremers/visual-fill-column][visual-fill-column]] to center =org-mode= buffers for a more pleasing writing experience as it centers the contents of the buffer horizontally to seem more like you are editing a document.  This is really a matter of personal preference so you can remove the block below if you don't like the behavior.

#+begin_src emacs-lisp

  (defun efs/org-mode-visual-fill ()
    (setq visual-fill-column-width 100 visual-fill-column-center-text t)
    (visual-fill-column-mode 1))

  (use-package visual-fill-column
    :hook
    (org-mode . efs/org-mode-visual-fill))

#+end_src

** Configure Babel Languages

To execute or export code in =org-mode= code blocks, you'll need to set up =org-babel-load-languages= for each language you'd like to use.  [[https://orgmode.org/worg/org-contrib/babel/languages.html][This page]] documents all of the languages that you can use with =org-babel=.

#+begin_src emacs-lisp

  (org-babel-do-load-languages 'org-babel-load-languages
                               '((emacs-lisp . t)))

#+end_src

** Structure Templates

Org Mode's [[https://orgmode.org/manual/Structure-Templates.html][structure templates]] feature enables you to quickly insert code blocks into your Org files in combination with =org-tempo= by typing =<= followed by the template name like =el= or =py= and then press =TAB=.  For example, to insert an empty =emacs-lisp= block below, you can type =<el= and press =TAB= to expand into such a block.

You can add more =src= block templates below by copying one of the lines and changing the two strings at the end, the first to be the template name and the second to contain the name of the language [[https://orgmode.org/worg/org-contrib/babel/languages.html][as it is known by Org Babel]].

#+begin_src emacs-lisp

  ;; This is needed as of Org 9.2
  (when (require 'org-tempo nil 'noerror)
    (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp")))

#+end_src

** Auto-tangle Configuration Files

This snippet adds a hook to =org-mode= buffers so that =efs/org-babel-tangle-config= gets executed each time such a buffer gets saved.  This function checks to see if the file being saved is the Emacs.org file you're looking at right now, and if so, automatically exports the configuration here to the associated output files.

#+begin_src emacs-lisp

  ;; Automatically tangle our Emacs.org config file when we save it
  (defun efs/org-babel-tangle-config ()
    (when (string-equal (buffer-file-name) (expand-file-name "~/.emacs.d/emacs.org"))
      ;; Dynamic scoping to the rescue
      (let ((org-confirm-babel-evaluate nil))
        (org-babel-tangle))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'efs/org-babel-tangle-config)))

#+end_src

* Development

** Projectile

[[https://projectile.mx/][Projectile]] is a project management library for Emacs which makes it a lot easier to navigate around code projects for various languages.  Many packages integrate with Projectile so it's a good idea to have it installed even if you don't use its commands directly.

#+begin_src emacs-lisp

  (use-package projectile
    :diminish projectile-mode
    :custom (projectile-completion-system 'ivy) 
    :bind-keymap ("C-c p" . projectile-command-map)
    :init
    ;; NOTE: Set this to the folder where you keep your Git repos!
    (when (file-directory-p "~/Projects/Code")
      (setq projectile-project-search-path '("~/Projects/Code"))
      (setq projectile-switch-project-action #'projectile-dired))
    :config
    (projectile-mode))

    (use-package counsel-projectile
      :config
      (counsel-projectile-mode))

#+end_src

** Magit

[[https://magit.vc/][Magit]] is the best Git interface I've ever used.  Common Git operations are easy to execute quickly using Magit's command panel system.

#+begin_src emacs-lisp

  ;; set magit package after forge so forge-add-default-bindings are disabled in magit
  (use-package magit
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))

  ;; NOTE: Make sure to configure a GitHub token before using this package!
  ;; - https://magit.vc/manual/forge/Token-Creation.html#Token-Creation
  ;; - https://magit.vc/manual/ghub/Getting-Started.html#Getting-Started
  (use-package forge) 

#+end_src

** Commenting

Emacs' built in commenting functionality =comment-dwim= (usually bound to =M-;=) doesn't always comment things in the way you might expect so we use [[https://github.com/redguardtoo/evil-nerd-commenter][evil-nerd-commenter]] to provide a more familiar behavior.  I've bound it to =M-/= since other editors sometimes use this binding but you could also replace Emacs' =M-;= binding with this command.

#+begin_src emacs-lisp

  (use-package evil-nerd-commenter
    :bind ("M-/" . evilnc-comment-or-uncomment-lines))

#+end_src

** Rainbow Delimiters

[[https://github.com/Fanael/rainbow-delimiters][rainbow-delimiters]] is useful in programming modes because it colorizes nested parentheses and brackets according to their nesting depth.  This makes it a lot easier to visually match parentheses in Emacs Lisp code without having to count them yourself.

#+begin_src emacs-lisp

  (use-package rainbow-delimiters
    :hook
    (prog-mode . rainbow-delimiters-mode))

#+end_src

* IDE Features with lsp-mode

** lsp-mode

We use the excellent [[https://emacs-lsp.github.io/lsp-mode/][lsp-mode]] to enable IDE-like functionality for many different programming languages via "language servers" that speak the [[https://microsoft.github.io/language-server-protocol/][Language Server Protocol]].  Before trying to set up =lsp-mode= for a particular language, check out the [[https://emacs-lsp.github.io/lsp-mode/page/languages/][documentation for your language]] so that you can learn which language servers are available and how to install them.

The =lsp-keymap-prefix= setting enables you to define a prefix for where =lsp-mode='s default keybindings will be added.  I *highly recommend* using the prefix to find out what you can do with =lsp-mode= in a buffer.

The =which-key= integration adds helpful descriptions of the various keys so you should be able to learn a lot just by pressing =C-c l= in a =lsp-mode= buffer and trying different things that you find there.

#+begin_src emacs-lisp

  ;; required by lsp-mode
  (use-package yasnippet)

  (defun efs/lsp-mode-setup ()
    (setq lsp-headerline-breadcrumb-segments '(path-up-to-project file symbols))
    (lsp-headerline-breadcrumb-mode))

  ;; clangd setup https://emacs-lsp.github.io/lsp-mode/page/lsp-clangd/ 
  (defun efs/lsp-clients-exe-setup ()
    (setq lsp-clients-clangd-executable (car (get_file_using_regex "LLVM" "clangd.exe"))))

  (defun efs/lsp-clients-lib-setup ()
    (add-to-list 'lsp-clients-clangd-library-directories (car (get_directories_using_regex "gcc"))))

  (use-package lsp-mode
    :commands (lsp lsp-deferred)
    :init
    ;; set prefix for lsp-command-keymap (few alternatives - "C-l", "C-c l")
    (setq lsp-keymap-prefix "C-c l")
    (efs/lsp-clients-exe-setup)
    :hook
    (c-mode . lsp-deferred)
    (c++-mode . lsp-deferred)
    ;; if you want which-key integration
    (lsp-mode . lsp-enable-which-key-integration)
    (lsp-mode . efs/lsp-mode-setup)
    (lsp-mode . efs/lsp-clients-lib-setup)
    :config
    ;; The path to lsp-mode needs to be added to load-path as well as the
    ;; path to the `clients' subdirectory.
    (add-to-list 'load-path (expand-file-name "lib/lsp-mode" user-emacs-directory))
    (add-to-list 'load-path (expand-file-name "lib/lsp-mode/clients" user-emacs-directory)))

#+end_src

*** lsp-ui

[[https://emacs-lsp.github.io/lsp-ui/][lsp-ui]] is a set of UI enhancements built on top of =lsp-mode= which make Emacs feel even more like an IDE.  Check out the screenshots on the =lsp-ui= homepage (linked at the beginning of this paragraph) to see examples of what it can do.

#+begin_src emacs-lisp

  (use-package lsp-ui
    :custom
    (lsp-ui-doc-position 'bottom)
    :hook
    (lsp-mode . lsp-ui-mode))

#+end_src

*** lsp-treemacs

[[https://github.com/emacs-lsp/lsp-treemacs][lsp-treemacs]] provides nice tree views for different aspects of your code like symbols in a file, references of a symbol, or diagnostic messages (errors and warnings) that are found in your code.

Try these commands with =M-x=:

- =lsp-treemacs-symbols= - Show a tree view of the symbols in the current file
- =lsp-treemacs-references= - Show a tree view for the references of the symbol under the cursor
- =lsp-treemacs-error-list= - Show a tree view for the diagnostic messages in the project

This package is built on the [[https://github.com/Alexander-Miller/treemacs][treemacs]] package which might be of some interest to you if you like to have a file browser at the left side of your screen in your editor.

#+begin_src emacs-lisp

  (defun efs/treemacs-config ()
    (setq treemacs-python-executable (car (get_file_using_regex "chocolatey" "python[0-9]?.[0-9]+?.exe"))))

  (defun efs/toggle-treemacs ()
    (treemacs)
    (lsp-treemacs-symbols))

  (use-package lsp-treemacs
    :init
    (efs/treemacs-config)
    :hook
    (lsp-mode . efs/toggle-treemacs)
    :config
    (lsp-treemacs-sync-mode t))

#+end_src

*** lsp-ivy

[[https://github.com/emacs-lsp/lsp-ivy][lsp-ivy]] integrates Ivy with =lsp-mode= to make it easy to search for things by name in your code.  When you run these commands, a prompt will appear in the minibuffer allowing you to type part of the name of a symbol in your code.  Results will be populated in the minibuffer so that you can find what you're looking for and jump to that location in the code upon selecting the result.

Try these commands with =M-x=:

- =lsp-ivy-workspace-symbol= - Search for a symbol name in the current project workspace
- =lsp-ivy-global-workspace-symbol= - Search for a symbol name in all active project workspaces

#+begin_src emacs-lisp

  (use-package lsp-ivy)

#+end_src

*** Company Mode

[[http://company-mode.github.io/][Company Mode]] provides a nicer in-buffer completion interface than =completion-at-point= which is more reminiscent of what you would expect from an IDE.  We add a simple configuration to make the keybindings a little more useful (=TAB= now completes the selection and initiates completion at the current location if needed).

We also use [[https://github.com/sebastiencs/company-box][company-box]] to further enhance the look of the completions with icons and better overall presentation.

#+begin_src emacs-lisp

  (use-package company
    :custom
    (company-minimum-prefix-length 1)
    (company-idle-delay 0.0)
    :bind
    (:map company-active-map
          ("<tab>" . company-complete-selection))
    (:map lsp-mode-map
          ("<tab>" . company-indent-or-complete-common))
    :hook
    (lsp-mode . company-mode))

  (use-package company-box
    :hook
    (company-mode . company-box-mode))

  (defun efs/company-c-headers-config ()
    (add-to-list 'company-c-headers-path-system (car (get_directories_using_regex "gcc"))))

  (use-package company-c-headers
    :config
    (add-to-list 'company-backends 'company-c-headers)
    (efs/company-c-headers-config))

#+end_src

** dap-mode

[[https://emacs-lsp.github.io/dap-mode/][dap-mode]] is an excellent package for bringing rich debugging capabilities to Emacs via the [[https://microsoft.github.io/debug-adapter-protocol/][Debug Adapter Protocol]]. You should check out the [[https://emacs-lsp.github.io/dap-mode/page/configuration/][configuration docs]] to learn how to configure the debugger for your language.  Also make sure to check out the documentation for the debug adapter to see what configuration parameters are available to use for your debug templates!

#+begin_src emacs-lisp

  (defun efs/dap-mode-configs ()
    (dap-ui-mode 1)
    (dap-tooltip-mode 1)
    (tooltip-mode 1)
    (dap-ui-controls-mode 1)) 

  (defun efs/dap-cpptools ()
    ;; additional docs: [[https://docs.doomemacs.org/latest/modules/tools/debugger/][blog]]   
    (when (require 'dap-cpptools nil 'noerror)
      (dap-cpptools-setup)))

  (defun find_debugger_path ()
    (car (get_file_using_regex "LLVM" "lldb.exe")))

  (defun init-dap-json (app)
    (interactive "sApplication name: ")
    (with-temp-file "launch.json"
        (insert (format "{
      \"version\": \"0.2.0\",
      \"configurations\": [
          {
              \"name\": \"Debug Emacs\",
              \"type\": \"cppdbg\",
              \"request\": \"launch\",
              \"program\": \"${workspaceFolder}/out/build/bin/%s/%s.exe\",
              \"args\": [\"-q\"],
              \"stopAtEntry\": false,
              \"cwd\": \"${workspaceFolder}\",
              \"environment\": [],
              \"externalConsole\": false,
              \"MIMode\": \"%s\",
              \"MIDebuggerPath\": %S
          }
      ]
  }" app app "lldb" (find_debugger_path))))) ;; Special case - %S MIDebuggerPath keep "\\"

  (use-package dap-mode
    :hook
    (lsp-deferred . dap-mode)
    (dap-mode . efs/dap-cpptools)
    (dap-mode . efs/dap-mode-configs)
    :config
    (add-hook 'dap-stopped-hook (lambda (arg) (call-interactively #'dap-hydra))))

#+end_src

** cmake-mode

Provides syntax highlighting and indentation for CMakeLists.txt and *.cmake source files.

#+begin_src emacs-lisp

  (use-package cmake-mode)

#+end_src

**** cmake-ide

cmake-ide is a package to enable IDE-like features on Emacs for CMake projects. It also supports non-CMake projects as long as a compilation database is generated out-of-band.This includes autocompletion and on-the-fly syntax checking in Emacs for CMake projects with minimal configuration.

#+begin_src emacs-lisp

  ;; required by cmake-ide
  ;; (use-package rtags)

  ;; (use-package cmake-ide
  ;;   :config
  ;;   (cmake-ide-setup))

#+end_src
